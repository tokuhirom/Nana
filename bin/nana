#!/usr/bin/perl
use strict;
use warnings;
use autodie;
use 5.10.1;
use Nana::Translator::Perl;
use Nana::Parser;
use Getopt::Long::Descriptive;
use Data::Dumper;

my ($opt, $usage) = describe_options(
    'nana %o <some-arg>',
    [],
    [ 'Dperl', "Dump perl code"   ],
    [ 'Dtree|Dt',   "Dump AST" ],
    [ 'eval|e=s',   "Eval expression" ],
    [ 'help|h',       "print usage message and exit" ],
);
print($usage) if $opt->help;

my $parser = Nana::Parser->new;
my $compiler = Nana::Translator::Perl->new();
if (defined $opt->eval) {
    my $ast = $parser->parse($opt->eval);
    say(Dumper($ast)) if $opt->dtree;
    my $perl = $compiler->compile($ast);
    say($perl) if $opt->dperl;
    eval $perl;
    die $@ if $@;
} elsif (@ARGV) {
    my $fname = shift @ARGV;
    open my $fh, '<', $fname;
    my $src = $parser->parse(do { local $/; <$fh>; });
    my $perl = $compiler->compile($src);
    say($perl) if $opt->dperl;
    eval $perl;
    die $@ if $@;
} else {
    if (-t STDIN && -t STDOUT) {
        while (1) {
            print(">> ");
            last if eof(STDIN);
            my $input = <>;

            my $ast = $parser->parse($input);
            my $perl = $compiler->compile($ast);
            say("PERL: $perl") if $opt->dperl;
            my $ret = eval $perl;
            die $@ if $@;
            local $Data::Dumper::Terse = 1;
            warn Dumper($ret);
        }
    } else {
        my $input = join('', <>);
        my $ast = $parser->parse($input);
        my $perl = $compiler->compile($ast);
        eval $perl;
        die $@ if $@;
    }
}

